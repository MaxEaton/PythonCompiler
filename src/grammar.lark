%import common.CNAME                      -> NAME
%import common.INT                        -> DECIMAL_INT
%import common.WS_INLINE
%ignore WS_INLINE
%declare _INDENT _DEDENT

NEWLINE: (/\n[\t ]*/)+
WHILE.1: "while"
IF.1: "if"
ELSE.1: "else"
DEF.1: "def"
LAMBDA.1: "lambda"
RETURN.1: "return"
AND.1: "and"
OR.1: "or"
NOT.1: "not"
IS.1: "is"
BREAK.1: "break"
TRUE.1: "True"
FALSE.1: "False"

start: module

module: stmt*

stmt: expr NEWLINE
    | NAME "=" obj NEWLINE                -> assign_
    | subscription "=" obj NEWLINE        -> assign_
    | listionary "=" obj NEWLINE          -> assign_
    | RETURN obj                          -> return_
    | BREAK NEWLINE
    | if_block
    | while_block
    | func_block
    | NEWLINE                             -> empty

expr: obj

obj: lmd "(" list_data ")"                -> func_
   | lmd 

lmd: LAMBDA args_data":" obj              -> lmd_
   | ternary

ternary: or_expr IF or_expr ELSE obj      -> ternary_
       | or_expr

or_expr: or_expr OR and_expr              -> or_
       | and_expr

and_expr: and_expr AND comp_expr          -> and_
        | comp_expr

comp_expr: comp_expr comp_op not_expr     -> comp_
         | not_expr

not_expr: NOT not_expr                    -> not_
        | add_expr

add_expr: add_expr "+" neg_expr           -> add_
        | neg_expr

neg_expr: "-" neg_expr                    -> neg_
        | term

term: "(" obj ")"  
    | DECIMAL_INT          
    | NAME          
    | TRUE                                -> true_
    | FALSE                               -> false_
    | func              
    | subscription
    | big

func: (NAME | func) "(" list_data ")"     -> func_

comp_op: "=="                             -> eq_
       | "!="                             -> ne_
       | IS                               -> is_

subscription: term "[" obj "]"

big: dictionary
   | listionary

dictionary: "{" dict_data "}"

dict_data:                                -> empty
         | obj ":" obj                    -> dict_datum
         | dict_data "," obj ":" obj

listionary: "[" list_data "]"

list_data:                                -> empty
         | obj                            -> list_datum
         | list_data "," obj

args_data:                                -> empty
         | NAME                           -> arg_
         | args_data "," obj

suite: _INDENT stmt+ _DEDENT

if_block: IF obj ":" NEWLINE suite [ELSE ":" NEWLINE suite]

while_block: WHILE obj ":" NEWLINE suite [ELSE ":" NEWLINE suite]

func_block: DEF NAME"(" args_data ")" ":" NEWLINE suite
